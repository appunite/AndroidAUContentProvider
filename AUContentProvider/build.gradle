buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    mavenCentral()
    maven {url 'https://commondatastorage.googleapis.com/maven-repository/'}
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'com.googlesource.gerrit:gs-maven-wagon:3.3.1'
    compile fileTree(dir: 'libs', include: '*.jar')
}

def baseVersionName = "1.2.2"
if (project.hasProperty("versionSuffix")) {
    project.ext.versionSuffix = project.versionSuffix
} else {
    project.ext.versionSuffix = "snapshot"
}
if (project.ext.versionSuffix) {
    version = "${baseVersionName}-${project.versionSuffix}"
} else {
    version = "${baseVersionName}"
}
archivesBaseName = "aucontentprovider"
group = "com.appunite"

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
        xmlOutput file("${reporting.baseDir}/lint/lint-results.xml")
        htmlOutput file("${reporting.baseDir}/lint/lint-results.html")
    }
}

task createPomFile {
    project.ext.pomFile = pom {
        version = project.version
        artifactId = project.archivesBaseName
        groupId = project.group
        project {
            name 'AUContentProvider'
            description 'library allow easily create content providers.'
            url 'http://appunite.com'
            inceptionYear '2012'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                url "https://review.appunite.com/androidausyncer"
                connection "https://review.appunite.com/androidausyncer.git"
            }
            developers {
                developer {
                    name 'Jacek Marchwicki <jacek.marchwicki@gmail.com>'
                }
            }
        }
    }
}

task install(type: Upload, dependsOn: createPomFile) {
    description = "Upload archives to local repository"
    configuration = configurations.archives
    repositories.mavenInstaller {
        pom = project.ext.pomFile
    }
}

Properties props = new Properties()
def credentialsProperties = file("credentials.properties")
if (credentialsProperties.exists()) {
        props.load(new FileInputStream(credentialsProperties))
}

uploadArchives {
    description = "Upload archives to maven repository"
    dependsOn createPomFile
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "gs://maven-repository") {
                authentication(
                userName: "${props.getProperty('mavenUser', null)}" ,
                password: "${props.getProperty('mavenPassword', null)}")
            }
            pom = project.ext.pomFile
        }
    }
}

